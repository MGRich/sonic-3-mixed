object 0x34;

var Activated:bool;
var Spin:int;
var Blinking:bool;

event Create() {
    Active = true;
    Priority = false;

    W = 16;
    H = 64;
    Rotation = 0;

    Activated = false;
    Spin = 0;
    Blinking = false;
    this.Frame = 0;
    CleanupInactiveObject = true;

    // TODO: Go through level editor and move all StarPosts out of the ground
}
event Update() {
    // 2 full rotations, counter clockwise, takes 32 frames
    // blinks every 4 frames
    for (var i:int = 0; i < 20; i++) {
        if (Scene.CollisionAt(this.X, this.Y + H / 2 - 1)) {
            this.Y -= 1;
        }
    }

    if (Scene.Checkpoint > this.SubType)
        this.Blinking = true;

    if (this.Activated && this.Spin < 32)
        this.Spin++;

    if (this.Spin == 32)
        this.Blinking = true;

    if (this.Blinking)
        this.Frame = ((Scene.Frame >> 2) & 0x1);
}

event OnLeaveScreen() : int {
    if (this.Activated && this.Spin < 32)
        this.Spin = 32;
    return 1;
}

event DrawStar(Rock:int, Ang:int, Side:int, CamX:int, CamY:int) {
    var cosval:int = (Math.cosHex(Rock) * 16) >> 16;
    var xx:int = (cosval * Math.cosHex(Ang)) >> 16;
    var yy:int = (cosval * Math.sinHex(Ang)) >> 16;

    if (Math.sinHex(Rock) * Side >= 0) {
        G.DrawSprite(Sprite,
            Sprite.Animations[11].Frames[0].X,
            Sprite.Animations[11].Frames[0].Y,
            Sprite.Animations[11].Frames[0].W,
            Sprite.Animations[11].Frames[0].H,
            X - CamX + xx, Y - CamY + yy, 0,
            IE_NOFLIP,
            Sprite.Animations[11].Frames[0].OffX,
            Sprite.Animations[11].Frames[0].OffY);
    }
}

event Render(CamX:int, CamY:int) {
    G.DrawSprite(Sprite, 8, 0, this.X - CamX, this.Y - CamY, 0, IE_NOFLIP);

    G.DrawSprite(Sprite, 9 + Frame, Frame, this.X - CamX + (Math.sinHex(this.Spin << 4) >> 13), this.Y - CamY - 16 - (Math.cosHex(this.Spin << 4) >> 13), 0, IE_NOFLIP);
}

event OnCollisionWithPlayer(PlayerID:int, HitFrom:int, Data:int) : int {
    if (!this.Blinking)
        this.Activated = true;

    if (Scene.Checkpoint < this.SubType) {
        Scene.Checkpoint = this.SubType;
        Scene.SavedPositionX = X;
        Scene.SavedPositionY = Y + H / 2 - 16;

        Sound.Play(Sound.SFX_STARPOST);
    }

    return 1;
}
